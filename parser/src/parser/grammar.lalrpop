use crate::{
    ast::{
        boundary_constraints::{Boundary, BoundaryConstraints, BoundaryConstraint, BoundaryExpr}, 
        transition_constraints::{TransitionConstraint, TransitionConstraints, TransitionExpr},
        constants::{Constant, ConstantType},
        Identifier, Source, SourceSection, TraceCols, PublicInput, PeriodicColumn
    }, error::{Error, ParseError::{InvalidInt, InvalidTraceCols, MissingMainTraceCols}}, lexer::Token
};
use std::str::FromStr;
use lalrpop_util::ParseError;

grammar;

// AST NODE
// ================================================================================================

pub Source: Source = {
    SourceSection* => Source(<>)
}

SourceSection: SourceSection = {
    AirDef => SourceSection::AirDef(<>),
    Constants => SourceSection::Constants(<>),
    TraceCols => SourceSection::TraceCols(<>),
    PublicInputs => SourceSection::PublicInputs(<>),
    PeriodicColumns => SourceSection::PeriodicColumns(<>),
    BoundaryConstraints => SourceSection::BoundaryConstraints(<>),
    TransitionConstraints => SourceSection::TransitionConstraints(<>),
}

// AIR DEF
// ================================================================================================

AirDef: Identifier = {
    "def" <f: Identifier> => f
}

// TRACE COLUMNS
// ================================================================================================

TraceCols: TraceCols = {
    "trace_columns" ":" <main_cols: MainCols?> <aux_cols: AuxCols?> =>? match (main_cols, aux_cols)
    {
        (Some(main_cols), Some(aux_cols)) => Ok(TraceCols { main_cols, aux_cols }),
        (Some(main_cols), None) => Ok(TraceCols { main_cols, aux_cols: vec![] }),
        (None, Some(_aux_cols)) => Err(ParseError::User {
            error: Error::ParseError(MissingMainTraceCols("Declaration of main trace columns is required".to_string()))
        }),
        (None, None) => Err(ParseError::User {
            error: Error::ParseError(InvalidTraceCols("Trace Columns cannot be empty".to_string()))
        })
    } 
}

MainCols: Vec<Identifier> = {
    "main" ":" "[" <main_cols: CommaElems<Identifier>> "]" => main_cols,
}

AuxCols: Vec<Identifier> = {
    "aux" ":" "[" <aux_cols: CommaElems<Identifier>> "]" => aux_cols,
}

// CONSTANTS
// ================================================================================================

// Constants section is optional but cannot be empty. Atleast one constant is required.
Constants: Vec<Constant> = {
    "constants" ":" <constants: Constant+> => constants
}

Constant: Constant = {
    <name: Identifier> ":" <scalar_value: Num_u64> => 
        Constant::new(name, ConstantType::Scalar(scalar_value)),
    <name: Identifier> ":" "[" <vector_value: CommaElems<Num_u64>> "]" =>
        Constant::new(name, ConstantType::Vector(vector_value)),
    <name: Identifier> ":" "[" <matrix_value: CommaElems<Vector<Num_u64>>> "]" =>
        Constant::new(name, ConstantType::Matrix(matrix_value)),
}

// PUBLIC INPUTS
// ================================================================================================

// At least one public input is required.
PublicInputs: Vec<PublicInput> = {
    "public_inputs" ":" <pub_inputs: PublicInput+> => pub_inputs
}

PublicInput: PublicInput = {
    <name: Identifier> ":" "[" <size: Num_u64> "]" => PublicInput::new(name, size),
}

// PERIODIC COLUMNS
// ================================================================================================

// Periodic columns are not required, and there is no limit to the number that can be provided.
PeriodicColumns: Vec<PeriodicColumn> = {
    "periodic_columns" ":" <periodic_columns: PeriodicColumn*> => periodic_columns
}

PeriodicColumn: PeriodicColumn = {
    <name: Identifier> ":" "[" <values: CommaElems<Num_u64>> "]" => PeriodicColumn::new(name, values),
}

// BOUNDARY CONSTRAINTS
// ================================================================================================

BoundaryConstraints: BoundaryConstraints = {
    "boundary_constraints" ":" <boundary_constraints: BoundaryConstraint+> =>
        BoundaryConstraints { boundary_constraints }
}

BoundaryConstraint: BoundaryConstraint = {
    "enf" <column: Identifier> "." <boundary: Boundary> "=" <value: BoundaryExpr> =>
        BoundaryConstraint::new(column, boundary, value)
}

Boundary: Boundary = {
    "first" => Boundary::First,
    "last" => Boundary::Last
}

// --- BOUNDARY CONSTRAINT EXPRESSIONS WITH PRECEDENCE (LOWEST TO HIGHEST) ----------------------

BoundaryExpr: BoundaryExpr = {
    <lexpr: BoundaryExpr> "+" <rexpr: BoundaryFactor> => BoundaryExpr::Add(Box::new(lexpr), Box::new(rexpr)),
    <lexpr: BoundaryExpr> "-" <rexpr: BoundaryFactor> => BoundaryExpr::Sub(Box::new(lexpr), Box::new(rexpr)),
    BoundaryFactor
}

BoundaryFactor: BoundaryExpr = {
    <lexpr: BoundaryFactor> "*" <rexpr: BoundaryAtom> => BoundaryExpr::Mul(Box::new(lexpr), Box::new(rexpr)),
    BoundaryAtom
}

BoundaryAtom: BoundaryExpr = {
    "(" <BoundaryExpr> ")",
    <lexpr: BoundaryAtom> "^" <num: Num_u64> => BoundaryExpr::Exp(Box::new(lexpr), num),
    "$rand" "[" <n: Num_u64> "]" => BoundaryExpr::Rand(n as usize),
    <n: Num_u64> => BoundaryExpr::Const(n),
    <ident: Identifier> => BoundaryExpr::Var(ident),
    <ident: Identifier> "[" <idx: Num_u64> "]" => BoundaryExpr::VecElem(ident, idx as usize),
    <ident: Identifier> "[" <idx_row: Num_u64> "]" "[" <idx_col: Num_u64> "]" =>
        BoundaryExpr::MatrixElem(ident, idx_row as usize, idx_col as usize)
}

// TRANSITION CONSTRAINTS
// ================================================================================================

TransitionConstraints: TransitionConstraints = {
    "transition_constraints" ":" <transition_constraints: TransitionConstraint+> =>
        TransitionConstraints { transition_constraints }
}

TransitionConstraint: TransitionConstraint = {
    "enf" <lhs: TransitionExpr> "=" <rhs: TransitionExpr> => TransitionConstraint::new(lhs, rhs)
}

// --- TRANSITION CONSTRAINT EXPRESSIONS WITH PRECEDENCE (LOWEST TO HIGHEST) ----------------------

TransitionExpr: TransitionExpr = {
    <lexpr: TransitionExpr> "+" <rexpr: TransitionFactor> => TransitionExpr::Add(Box::new(lexpr), Box::new(rexpr)),
    <lexpr: TransitionExpr> "-" <rexpr: TransitionFactor> => TransitionExpr::Sub(Box::new(lexpr), Box::new(rexpr)),
    TransitionFactor
}

TransitionFactor: TransitionExpr = {
    <lexpr: TransitionFactor> "*" <rexpr: TransitionAtom> => TransitionExpr::Mul(Box::new(lexpr), Box::new(rexpr)),
    TransitionAtom
}

TransitionAtom: TransitionExpr = {
    "(" <TransitionExpr> ")",
    <lexpr: TransitionAtom> "^" <num: Num_u64> => TransitionExpr::Exp(Box::new(lexpr), num),
    "$rand" "[" <n: Num_u64> "]" => TransitionExpr::Rand(n as usize),
    <n: Num_u64> => TransitionExpr::Const(n),
    <ident: Identifier> => TransitionExpr::Var(ident),
    <ident: Identifier> "[" <idx: Num_u64> "]" => TransitionExpr::VecElem(ident, idx as usize),
    <ident: Identifier> "[" <idx_row: Num_u64> "]" "[" <idx_col: Num_u64> "]" =>
        TransitionExpr::MatrixElem(ident, idx_row as usize, idx_col as usize),
    <ident: Identifier> "'" => TransitionExpr::Next(ident)
}

// ATOMS
// ================================================================================================

CommaElems<T>: Vec<T> = {
    <e:T> <v:("," <T>)*> => {
        let mut v = v;
        v.insert(0, e);
        v
    }
}

Vector<T>: Vec<T> = {
    "[" <e:T> <v:("," <T>)*> "]" => {
        let mut v = v;
        v.insert(0, e);
        v
    }
}

Identifier: Identifier = {
    <n:identifier> => Identifier(n.to_string())
}

Num_u64: u64 = {
    r"[0-9]+" =>? u64::from_str(&<>)
        .map_err(|_| ParseError::User {
            error: Error::ParseError(InvalidInt(format!("Int too big : {}", <>).to_string()))
        })
}

// LEXER
// ================================================================================================

extern {
    type Error = Error;
    type Location = usize;

    enum Token {
        identifier => Token::Ident(<String>),
        r"[0-9]+" => Token::Num(<String>),
        "def" => Token::Def,
        "trace_columns" => Token::TraceColumnns,
        "constants" => Token::Constants,
        "main" => Token::Main,
        "aux" => Token::Aux,
        "public_inputs" => Token::PublicInputs,
        "periodic_columns" => Token::PeriodicColumns,
        "boundary_constraints" => Token::BoundaryConstraints,
        "first" => Token::First,
        "last" => Token::Last,
        "transition_constraints" => Token::TransitionConstraints,
        "$rand" => Token::Rand,
        "enf" => Token::Enf,
        "'" => Token::Next,
        "=" => Token::Equal,
        "+" => Token::Plus,
        "-" => Token::Minus,
        "*" => Token::Mul,
        "^" => Token::Exp,
        ":" => Token::Colon,
        "," => Token::Comma,
        "[" => Token::Lsqb,
        "]" => Token::Rsqb,
        "(" => Token::Lparen,
        ")" => Token::Rparen,
        "." => Token::Dot,
    }
}
