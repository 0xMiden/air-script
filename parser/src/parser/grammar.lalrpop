use crate::{
    ast::{
        boundary_constraints::{Boundary, BoundaryConstraints, BoundaryConstraint, BoundaryExpr}, 
        transition_constraints::{TransitionConstraint, TransitionConstraints, TransitionExpr},
        Identifier, Source, SourceSection, TraceCols, PublicInput, PeriodicColumn
    }, error::{Error, ParseError::{InvalidInt, InvalidTraceCols}}, lexer::Token
};
use std::str::FromStr;
use lalrpop_util::ParseError;

grammar;

// AST NODE
// ================================================================================================

pub Source: Source = {
    SourceSection* => Source(<>)
}

SourceSection: SourceSection = {
    AirDef => SourceSection::AirDef(<>),
    TraceCols => SourceSection::TraceCols(<>),
    PublicInputs => SourceSection::PublicInputs(<>),
    PeriodicColumns => SourceSection::PeriodicColumns(<>),
    BoundaryConstraints => SourceSection::BoundaryConstraints(<>),
    TransitionConstraints => SourceSection::TransitionConstraints(<>),
}

// AIR DEF
// ================================================================================================

AirDef: Identifier = {
    "def" <f: Identifier> => f
}

// TRACE COLUMNS
// ================================================================================================

TraceCols: TraceCols = {
    "trace_columns" ":" <main_cols: MainCols?> <aux_cols: AuxCols?> =>? match (main_cols, aux_cols)
    {
        (Some(main_cols), Some(aux_cols)) => Ok(TraceCols { main_cols, aux_cols }),
        (Some(main_cols), None) => Ok(TraceCols { main_cols, aux_cols: vec![] }),
        (None, Some(aux_cols)) => Ok(TraceCols { main_cols: vec![], aux_cols }),
        (None, None) => Err(ParseError::User {
            error: Error::ParseError(InvalidTraceCols("Trace Columns cannot be empty".to_string()))
        })
    } 
}

MainCols: Vec<Identifier> = {
    "main" ":" "[" <main_cols: CommaElems<Identifier>> "]" => main_cols,
}

AuxCols: Vec<Identifier> = {
    "aux" ":" "[" <aux_cols: CommaElems<Identifier>> "]" => aux_cols,
}


// PUBLIC INPUTS
// ================================================================================================

// Public inputs are not required, and there is no limit to the number that can be provided.
PublicInputs: Vec<PublicInput> = {
    "public_inputs" ":" <pub_inputs: PublicInput*> => pub_inputs
}

PublicInput: PublicInput = {
    <name: Identifier> ":" "[" <size: Num_u64> "]" => PublicInput::new(name, size),
}

// PERIODIC COLUMNS
// ================================================================================================

// Public inputs are not required, and there is no limit to the number that can be provided.
PeriodicColumns: Vec<PeriodicColumn> = {
    "periodic_columns" ":" <periodic_columns: PeriodicColumn*> => periodic_columns
}

PeriodicColumn: PeriodicColumn = {
    <name: Identifier> ":" "[" <values: CommaElems<Num_u64>> "]" => PeriodicColumn::new(name, values),
}

// BOUNDARY CONSTRAINTS
// ================================================================================================

BoundaryConstraints: BoundaryConstraints = {
    "boundary_constraints" ":" <boundary_constraints: BoundaryConstraint+> =>
        BoundaryConstraints { boundary_constraints }
}

BoundaryConstraint: BoundaryConstraint = {
    "enf" <column: Identifier> "." <boundary: Boundary> "=" <value: BoundaryExpr> =>
        BoundaryConstraint::new(column, boundary, value)
}

Boundary: Boundary = {
    "first" => Boundary::First,
    "last" => Boundary::Last
}

// --- TRANSITION CONSTRAINT EXPRESSIONS WITH PRECEDENCE (LOWEST TO HIGHEST) ----------------------

BoundaryExpr: BoundaryExpr = {
    <lexpr: BoundaryExpr> "+" <rexpr: BoundaryAtom> => BoundaryExpr::Add(Box::new(lexpr), Box::new(rexpr)),
    <lexpr: BoundaryExpr> "-" <rexpr: BoundaryAtom> => BoundaryExpr::Subtract(Box::new(lexpr), Box::new(rexpr)),
    BoundaryAtom
}

BoundaryAtom: BoundaryExpr = {
    <n: Num_u64> => BoundaryExpr::Constant(n),
    <ident: Identifier> "[" <index: Num_u64> "]" => BoundaryExpr::PublicInput(ident, index as usize)
}

// TRANSITION CONSTRAINTS
// ================================================================================================

TransitionConstraints: TransitionConstraints = {
    "transition_constraints" ":" <transition_constraints: TransitionConstraint+> =>
        TransitionConstraints { transition_constraints }
}

TransitionConstraint: TransitionConstraint = {
    "enf" <lhs: TransitionExpr> "=" <rhs: TransitionExpr> => TransitionConstraint::new(lhs, rhs)
}

// --- TRANSITION CONSTRAINT EXPRESSIONS WITH PRECEDENCE (LOWEST TO HIGHEST) ----------------------

TransitionExpr: TransitionExpr = {
    <lexpr: TransitionExpr> "+" <rexpr: TransitionAtom> => TransitionExpr::Add(Box::new(lexpr), Box::new(rexpr)),
    <lexpr: TransitionExpr> "-" <rexpr: TransitionAtom> => TransitionExpr::Subtract(Box::new(lexpr), Box::new(rexpr)),
    TransitionAtom
}

TransitionAtom: TransitionExpr = {
    <n: Num_u64> => TransitionExpr::Constant(n),
    <s: Identifier> => TransitionExpr::Variable(s),
    <s: Identifier> "'" => TransitionExpr::Next(s),
    "$rand" "[" <n: Num_u64> "]" => TransitionExpr::RandomValue(n as usize)
}

// ATOMS
// ================================================================================================

CommaElems<T>: Vec<T> = {
    <e:T> <v:("," <T>)*> => {
        let mut v = v;
        v.insert(0, e);
        v
    }
}
Identifier: Identifier = {
    <n:identifier> => Identifier(n.to_string())
}

Num_u64: u64 = {
    r"[0-9]+" =>? u64::from_str(&<>)
        .map_err(|_| ParseError::User {
            error: Error::ParseError(InvalidInt(format!("Int too big : {}", <>).to_string()))
        })
}

// LEXER
// ================================================================================================

extern {
    type Error = Error;
    type Location = usize;

    enum Token {
        identifier => Token::Ident(<String>),
        r"[0-9]+" => Token::Number(<String>),
        "def" => Token::Def,
        "trace_columns" => Token::TraceColumnns,
        "main" => Token::Main,
        "aux" => Token::Aux,
        "public_inputs" => Token::PublicInputs,
        "periodic_columns" => Token::PeriodicColumns,
        "boundary_constraints" => Token::BoundaryConstraints,
        "first" => Token::First,
        "last" => Token::Last,
        "transition_constraints" => Token::TransitionConstraints,
        "$rand" => Token::Rand,
        "enf" => Token::Enf,
        "'" => Token::Next,
        "=" => Token::Equal,
        "+" => Token::Plus,
        "-" => Token::Minus,
        ":" => Token::Colon,
        "," => Token::Comma,
        "[" => Token::Lsqb,
        "]" => Token::Rsqb,
        "." => Token::Dot,
    }
}
